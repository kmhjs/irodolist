#! /usr/bin/env zsh

function _irodolist::is_zcl_available()
{
  [[ ${functions[(i)zcl]} == 'zcl' ]]
  return $?
}

function _irodolist::text_attribute_names()
{
  local -a text_attributes=(
    'normal'
    'bold'
  )

  echo -n ${text_attributes}
}

function _irodolist::is_valid_text_attribute_name()
{
  local -a text_attribute_names=($(_irodolist::text_attribute_names))
  local attribute_name=${1}

  [[ ${text_attribute_names[(r)${attribute_name}]} == ${attribute_name} ]]
  return $?
}

function _irodolist::text_attribute_name_to_ansi_color_id()
{
  local -a text_attribute_names=($(_irodolist::text_attribute_names))
  local attribute_name=${1}

  echo -n $((${text_attribute_names[(i)${attribute_name}]} - 1))
}

function _irodolist::text_attribute_name_to_ansi()
{
  local attribute_name=${1}

  if [[ ${attribute_name} == 'default' ]]
  then
    echo -n '00'
    return 0
  fi

  if ! _irodolist::is_valid_text_attribute_name ${attribute_name}
  then
    return 1
  fi

  echo -n "0$(_irodolist::text_attribute_name_to_ansi_color_id ${attribute_name})"

  return 0
}

function _irodolist::text_color_names()
{
  local -a text_color_names=(
    'black'
    'red'
    'green'
    'yellow'
    'blue'
    'magenta'
    'cyan'
    'white'
  )

  echo -n ${text_color_names}
}

function _irodolist::is_valid_text_color_name()
{
  local -a text_color_names=($(_irodolist::text_color_names))
  local color_name=${1}

  [[ ${text_color_names[(r)${color_name}]} == ${color_name} ]]
  return $?
}

function _irodolist::text_color_name_to_ansi_color_id()
{
  local -a text_color_names=($(_irodolist::text_color_names))
  local color_name=${1}

  echo -n $((${text_color_names[(i)${color_name}]} - 1))
}

function _irodolist::text_color_name_to_ansi()
{
  local color_name=${1}
  local color_type=${2} # fg or bg are expected

  if [[ ${color_name} == 'default' ]]
  then
    return 0
  fi

  # Validates color name
  if ! _irodolist::is_valid_text_color_name ${color_name}
  then
    return 1
  fi

  # Converts color into ansi color value
  local -i color_id=$(_irodolist::text_color_name_to_ansi_color_id ${color_name})
  case ${color_type} in
    fg)
      echo -n "3${color_id}"
      ;;
    bg)
      echo -n "4${color_id}"
      ;;
    *)
      return 1
      ;;
  esac

  return 0
}

function _irodolist::text_fg_color_name_to_ansi()
{
  local color_name=${1}

  _irodolist::text_color_name_to_ansi ${color_name} 'fg'
  return $?
}

function _irodolist::text_bg_color_name_to_ansi()
{
  local color_name=${1}

  _irodolist::text_color_name_to_ansi ${color_name} 'bg'
  return $?
}

function _irodolist::ansi_color()
{
  local fg_color_name=${1}
  local bg_color_name=${2}
  local text_attribute_name=${3}

  local attribute_ansi=$(_irodolist::text_attribute_name_to_ansi ${text_attribute_name})
  if [[ -n ${attribute_ansi} ]]
  then
    attribute_ansi+=';'
  fi

  local fg_ansi=$(_irodolist::text_fg_color_name_to_ansi ${fg_color_name})
  if [[ -n ${fg_ansi} ]]
  then
    fg_ansi+=';'
  fi

  local bg_ansi=$(_irodolist::text_bg_color_name_to_ansi ${bg_color_name})
  if [[ -n ${bg_ansi} ]]
  then
    bg_ansi+=';'
  fi

  echo -n "\e[${attribute_ansi}${fg_ansi}${bg_ansi}m"
}

function _irodolist::text_color_name_to_lscolor_value()
{
  local color_name=${1}
  local attribute_name=${2}

  if [[ ${color_name} == 'default' ]]
  then
    echo -n 'x'
    return 0
  fi

  # Validates color name
  if ! _irodolist::is_valid_text_color_name ${color_name}
  then
    return 1
  fi

  # Validates attribute
  if ! _irodolist::is_valid_text_attribute_name ${attribute_name}
  then
    return 1
  fi

  # Converts color into ansi color value
  local -i color_id=$(($(_irodolist::text_color_name_to_ansi_color_id ${color_name}) + 1))
  local -a lscolor_entries=({a..h})

  case ${attribute_name} in
    normal)
      echo -n ${(L)lscolor_entries[${color_id}]}
      ;;

    bold)
      echo -n ${(U)lscolor_entries[${color_id}]}
      ;;

    *) # Unexpected pattern
      ;;
  esac

  return 0
}

function _irodolist::lscolor()
{
  local fg_color_name=${1}
  local bg_color_name=${2}
  local text_attribute_name=${3}

  local fg_color=$(_irodolist::text_color_name_to_lscolor_value ${fg_color_name} ${text_attribute_name})
  local bg_color=$(_irodolist::text_color_name_to_lscolor_value ${bg_color_name} 'normal')

  echo -n "${fg_color}${bg_color}"
}

function _irodolist::color()
{
  local target=${1}
  local fg_color_name=${2}
  local bg_color_name=${3}
  local text_attribute_name=${4}

  case ${target} in
    --ansi)
      _irodolist::ansi_color ${fg_color_name} ${bg_color_name} ${text_attribute_name}
      ;;

    --lscolor)
      _irodolist::lscolor ${fg_color_name} ${bg_color_name} ${text_attribute_name}
      ;;

    *)
      echo "No conversion tasks were found for ${target}." 1>&2
      return 1
      ;;
  esac

  return 0
}
